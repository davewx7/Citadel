{
	id: "quest_display",
	is_strict: true,

	properties: {
		controller: { type: "obj title_controller" },

		quest: { type: "class quest" },

		_proportion_ratio: { type: "null|decimal", change: "render()" },
		_display_current_value: { type: "null|int" },

		animate_value: "def(int old_value, int new_value) ->commands
		if(old_value != new_value,
			set(_proportion_ratio, old_value/decimal(quest.max_value));
			set(_display_current_value, old_value);
			animate(me, {
				_proportion_ratio: new_value/decimal(quest.max_value),
				_display_current_value: new_value,
			}, {
				duration: 12,
				on_complete: [set(_proportion_ratio, null), set(_display_current_value, null)],
			})
		)
		",

		animate_quest_claimed: "def() ->commands
			set(_claimed, true); render()

		",

		_complete: "bool :: quest.current_value >= quest.max_value",
		_claimed: { type: "bool", default: false },

		render: "def() ->commands [

		set(animation, lib.citadel.render_frame(c, ww, hh,
		[
			c.set_source_rgba(0.1,0.12,0.15,1.0),
			c.rectangle(0, 0, ww, hh),
			c.fill(),

			c.set_line_width(lib.citadel.dpy(1.0)),
			c.set_source_rgba(0.28,0.30,0.32,1.0),
			c.rectangle(0, 0, ww, hh),
			c.stroke(),

			if(not _complete, [
				c.set_source_rgba(1,1,1,1),
				c.rectangle(ww*0.85, hh*0.2, ww*0.12, hh*0.02),
				c.fill(),

				c.set_source_rgba(0,1,1,1),
				c.rectangle(ww*0.85, hh*0.2, ww*0.12*bar_proportion, hh*0.02),
				c.fill(),
			]),

			c.set_source_rgba(1,1,1,1),

			map(fragments, [
				c.save(),
				c.translate(ww*0.18, hh*0.08),
				value.path,
				c.fill(),
				c.restore(),
			])
			where fragments = c.markup_text(q(<font size='16'><b>) + quest.name + if(_claimed, q(&emdash;Claiming...), _complete, q(&emdash;Claim Reward), q()) + q(</b></font>), {scale: hh/50.0, width: 1000}),

			map(fragments, [
				c.save(),
				c.translate(ww*0.18, min(hh*0.55, hh - fragments_height)),
				value.path,
				c.fill(),
				c.restore(),
			])
			where fragments_height = (max(map(fragments, value.y+value.height)) or 0)*1.15
			where fragments = c.markup_text(q(<font size='14'>) + quest.rules + q(</font>), {scale: hh/50.0, scale_line_heights: 0.75, width: ww*0.65}),

			map(fragments, [
				c.save(),
				c.translate(ww*0.035, hh*0.62),
				value.path,
				c.fill(),
				c.restore(),
			])
			where fragments = c.markup_text(q(<font size='12'>Gold</font>), {scale: hh/50.0, width: 1000}),

			map(fragments, [
				c.save(),
				c.translate(ww*if(quest.reward >= 100, 0.027, 0.035), hh*0.15),
				value.path,
				c.fill(),
				c.restore(),
			])
			where fragments = c.markup_text(q(<font size='18'><b>) + str(quest.reward) + q(</b></font>), {scale: hh/50.0, width: 1000}),

			if(not _complete,
				map(fragments, [
					c.save(),
					c.translate(ww - lib.citadel.py(30), hh/2 - value.height/2),
					value.path,
					c.fill(),
					c.restore(),
				])
				where fragments = c.markup_text(q(<font size='12'>) + str(if(_display_current_value != null, _display_current_value, quest.current_value)) + '/' + str(quest.max_value) + q(</font>), {scale: hh/50.0, width: 1000})
			),

		])) where c = canvas()
		    where ww = round_to_even(lib.citadel.py(206))
		    where hh = lib.citadel.py(46)
			where bar_proportion = if(_proportion_ratio != null, _proportion_ratio, quest.current_value / decimal(quest.max_value))
		]
		",
	},

	events: {
		create: "render()",
		type_updated: "render()",

		being_removed: "[
		]",

		click: "if(_complete and _claimed = false, [
			controller.claim_quest(quest.type); animate_quest_claimed()
		])",

		mouse_enter: "if(_complete and _claimed = false, set(brightness, 400))",
		mouse_leave: "if(_complete, set(brightness, 255))",
	},


}
