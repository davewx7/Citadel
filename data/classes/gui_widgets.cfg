{
	properties: {
		resource_image: "def(int school, int size=14) ->map {
			type: 'image',
			image: 'magic-icons.png',
			area: [4+(school-1)*17,4,18+(school-1)*17,18],
			image_width: size,
			image_height: size
		}",

		icon_image: "def(int index, int size=10) ->map {
			type: 'image',
			image: 'citadel-icons.png',
			area: [index*10,0,(index+1)*10,10],
			image_width: size,
			image_height: size
		}",
		
		number_image: "def(int index, int size=10) ->map {
			type: 'image',
			image: 'citadel-buildings.png',
			area: [117+index*10,266,116+(index+1)*10,275],
			image_width: size,
			image_height: size
		}",

		font: "'RobotoCondensed-Light'",

		icon_widget: "def(string str, int size=10) ->map
		switch(str,
			'&gold;',    resource_image(1, size),
			'&blood;',   resource_image(2, size),
			'&food;',    resource_image(3, size),
			'&scrolls;', resource_image(4, size),
			'&faith;',   resource_image(5, size),
			'&life;',    icon_image(0, size),
			'&vuln;',    icon_image(1, size),
			'&resist;',  icon_image(2, size),
			'&range;',   icon_image(3, size),
			'&attack;',  icon_image(4, size),
			'&exhaust;', icon_image(5, size),
			'&energy;',  icon_image(6, size),
			'&move;',    icon_image(7, size),
			'&mana;',    icon_image(8, size),
			'&0;',		 number_image(0, size),
			'&1;',		 number_image(1, size),
			'&2;',		 number_image(2, size),
			'&3;',		 number_image(3, size),
			'&4;',		 number_image(4, size),
			'&5;',		 number_image(5, size),
			'&6;',		 number_image(6, size),
			'&7;',		 number_image(7, size),
			'&8;',		 number_image(8, size),
			'&9;',		 number_image(9, size),
			'&+;',       number_image(10, size),
			'&-;',       number_image(11, size),
			{} asserting (not 'Bad markup in card') | str)",

		parse_markup_into_widgets: "def(string text, int size=14, color='black') ->[map]
		  if(match, [
			label_base + {text: match[0]},
			icon_widget(string<- match[1], size+2),
		  	] + parse_markup_into_widgets(string<- match[2], size, color),
		  	[label_base + {text: text}])
		  where match = regex_match(text, '(.*?)(&[a-z0-9\+\-]+;)(.*)')
		  where label_base = {
			type: 'label',
			color: color,
			size: size,
			font: font,
		  }",
	}
}
